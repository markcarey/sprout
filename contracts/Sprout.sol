// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/token/ERC721/ERC721Upgradeable.sol";

import "@openzeppelin/contracts-upgradeable/token/ERC721/extensions/ERC721EnumerableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC721/extensions/ERC721BurnableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC721/extensions/ERC721PausableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC721/extensions/ERC721URIStorageUpgradeable.sol";

import "@openzeppelin/contracts-upgradeable/access/AccessControlEnumerableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/utils/ContextUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/utils/CountersUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

import "@openzeppelin/contracts/proxy/Clones.sol";


// based on openzeppelin-contracts-upgradeable/contracts/token/ERC721/presets/ERC721PresetMinterPauserAutoIdUpgradeable.sol

/**
 * @dev {ERC721} token, including:
 *
 *  - ability for holders to burn (destroy) their tokens
 *  - a minter role that allows for token minting (creation)
 *  - a pauser role that allows to stop all token transfers
 *  - token ID and URI autogeneration
 *
 * This contract uses {AccessControl} to lock permissioned functions using the
 * different roles - head to its documentation for details.
 *
 * The account that deploys the contract will be granted the minter and pauser
 * roles, as well as the default admin role, which will let it grant both minter
 * and pauser roles to other accounts.
 */
contract Garden is
    Initializable, ContextUpgradeable,
    AccessControlEnumerableUpgradeable,
    ERC721EnumerableUpgradeable,
    ERC721BurnableUpgradeable,
    ERC721PausableUpgradeable,
    ERC721URIStorageUpgradeable
{
    function initialize(
        string memory name,
        string memory symbol,
        string memory baseTokenURI,
        address owner,
        string memory contractURI
    ) public virtual initializer {
        __ERC721PresetMinterPauserAutoId_init(name, symbol, baseTokenURI, owner, contractURI);
    }
    using CountersUpgradeable for CountersUpgradeable.Counter;

    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");
    bytes32 public constant PAUSER_ROLE = keccak256("PAUSER_ROLE");

    CountersUpgradeable.Counter private _tokenIdTracker;

    string private _baseTokenURI;
    string private _contractUri;

    /**
     * @dev Grants `DEFAULT_ADMIN_ROLE`, `MINTER_ROLE` and `PAUSER_ROLE` to the
     * account that deploys the contract.
     *
     * Token URIs will be autogenerated based on `baseURI` and their token IDs.
     * See {ERC721-tokenURI}.
     */
    function __ERC721PresetMinterPauserAutoId_init(
        string memory name,
        string memory symbol,
        string memory baseTokenURI,
        address owner,
        string memory contractURI
    ) internal initializer {
        __Context_init_unchained();
        __ERC165_init_unchained();
        __AccessControl_init_unchained();
        __AccessControlEnumerable_init_unchained();
        __ERC721_init_unchained(name, symbol);
        __ERC721Enumerable_init_unchained();
        __ERC721Burnable_init_unchained();
        __Pausable_init_unchained();
        __ERC721Pausable_init_unchained();
        // SPROUT: added for IPFS support
        __ERC721URIStorage_init_unchained();
        __ERC721PresetMinterPauserAutoId_init_unchained(name, symbol, baseTokenURI, owner, contractURI);
    }

    function __ERC721PresetMinterPauserAutoId_init_unchained(
        string memory name,
        string memory symbol,
        string memory baseTokenURI,
        address owner,
        string memory contractURI
    ) internal initializer {
        _baseTokenURI = baseTokenURI;
        _contractUri = contractURI;

        _setupRole(DEFAULT_ADMIN_ROLE, owner);

        _setupRole(MINTER_ROLE, owner);
        _setupRole(PAUSER_ROLE, owner);
    }

    function _baseURI() internal view virtual override returns (string memory) {
        return _baseTokenURI;
    }

    /**
     * @dev Creates a new token for `to`. Its token ID will be automatically
     * assigned (and available on the emitted {IERC721-Transfer} event), and the token
     * URI autogenerated based on the base URI passed at construction.
     *
     * See {ERC721-_mint}.
     *
     * Requirements:
     *
     * - the caller must have the `MINTER_ROLE`.
     */
    function mint(string calldata tokenURI) public virtual {
        require(hasRole(MINTER_ROLE, _msgSender()), "ERC721PresetMinterPauserAutoId: must have minter role to mint");

        // We cannot just use balanceOf to create the new tokenId because tokens
        // can be burned (destroyed), so we need a separate counter.
        _mint(_msgSender(), _tokenIdTracker.current());
        // SPROUT: added for IPFS support
        _setTokenURI(_tokenIdTracker.current(), tokenURI);
        _tokenIdTracker.increment();
    }

    function contractURI() public view returns (string memory) {
        return _contractUri;
    }

    function _burn(uint256 tokenId) internal override(ERC721Upgradeable, ERC721URIStorageUpgradeable) {
        require(hasRole(MINTER_ROLE, _msgSender()));
        super._burn(tokenId);
    }
    function tokenURI(uint256 tokenId) public view override(ERC721Upgradeable, ERC721URIStorageUpgradeable) 
            returns (string memory){
        return super.tokenURI(tokenId);
    }

    /**
     * @dev Pauses all token transfers.
     *
     * See {ERC721Pausable} and {Pausable-_pause}.
     *
     * Requirements:
     *
     * - the caller must have the `PAUSER_ROLE`.
     */
    function pause() public virtual {
        require(hasRole(PAUSER_ROLE, _msgSender()), "ERC721PresetMinterPauserAutoId: must have pauser role to pause");
        _pause();
    }

    /**
     * @dev Unpauses all token transfers.
     *
     * See {ERC721Pausable} and {Pausable-_unpause}.
     *
     * Requirements:
     *
     * - the caller must have the `PAUSER_ROLE`.
     */
    function unpause() public virtual {
        require(hasRole(PAUSER_ROLE, _msgSender()), "ERC721PresetMinterPauserAutoId: must have pauser role to unpause");
        _unpause();
    }

    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override(ERC721Upgradeable, ERC721EnumerableUpgradeable, ERC721PausableUpgradeable) {
        super._beforeTokenTransfer(from, to, tokenId);
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId)
        public
        view
        virtual
        override(AccessControlEnumerableUpgradeable, ERC721Upgradeable, ERC721EnumerableUpgradeable)
        returns (bool)
    {
        return super.supportsInterface(interfaceId);
    }
    uint256[48] private __gap;
}

contract FactoryClone {
    using StringsUpgradeable for uint;
    address immutable tokenImplementation;
    address owner;
    address[] public allGardens;
    mapping(address => address[]) userToGardens;
    mapping(address => address) ownerOfGarden;
    mapping (address => uint) pendingWithdrawals;
    mapping(address => address[]) userToFollowedGardens;

    constructor() public {
        tokenImplementation = address(new Garden());
        owner = msg.sender;
    }

    event GardenCreated(
        address indexed _owner,
        address indexed _contract,
        uint
    );

    event GardenFollowed(
        address indexed _follower,
        address indexed _contract
    );

    event PlantLiked(
        address indexed _liker,
        address indexed _contract,
        uint
    );

    function createGarden(string calldata name, string calldata symbol, string calldata baseTokenURI, string calldata contractURI) external payable returns (address) {
        require(msg.value == 0.1 ether, "You must send 0.1 MATIC");
        pendingWithdrawals[owner] += msg.value;
        address clone = Clones.clone(tokenImplementation);
        Garden(clone).initialize(name, symbol, baseTokenURI, msg.sender, contractURI);
        allGardens.push(clone);
        userToGardens[msg.sender].push(clone);
        ownerOfGarden[clone] = msg.sender;
        emit GardenCreated(msg.sender, clone, allGardens.length);
        return clone;
    }

    // returns array of all Garden contract addresses
    function getAllGardens() public view returns (address[] memory){
       return allGardens;
    }

    // returns array of all Garden contract addresses for a specified user address
    function getFollowsForUser(address user) public view returns (address[] memory){
       return userToFollowedGardens[user];
    }

    // returns array of all Garden contract addresses for a specified user address
    function getGardensForUser(address user) public view returns (address[] memory){
       return userToGardens[user];
    }

    function follow(address gardenAddress) external payable {
        require(msg.value == 0.1 ether, "You must send 0.1 MATIC");
        address gardener = ownerOfGarden[gardenAddress];
        pendingWithdrawals[gardener] += 0.09 ether;
        pendingWithdrawals[owner] += 0.01 ether;
        // TODO: track followers, following, plus counts
        userToFollowedGardens[msg.sender].push(gardenAddress);
        emit GardenFollowed(msg.sender, gardenAddress);
    }

    function like(address gardenAddress, uint256 tokenId) external payable  {
        require(msg.value == 0.01 ether, "You must send 0.01 MATIC");
        address gardener = ownerOfGarden[gardenAddress];
        pendingWithdrawals[gardener] += 0.009 ether;
        pendingWithdrawals[owner] += 0.001 ether;
        // TODO: track likers, likes, plus counts
        emit PlantLiked(msg.sender, gardenAddress, tokenId);
    }

    function claimable() public view returns (uint){
        uint amount = pendingWithdrawals[msg.sender];
        return amount;
    }

    function withdraw() public {
        uint amount = pendingWithdrawals[msg.sender];
        require(amount > 0 ether, "Nothing to claim");
        pendingWithdrawals[msg.sender] = 0;
        payable(msg.sender).transfer(amount);
    }

}